% source: https://ddmler.github.io/asp/2018/07/10/answer-set-programming-sudoku-solver.html

% defines 9x9 grid
x(1..9).
y(1..9).
n(1..9).

% every field contains a number from 1 to 9
{sudoku(X,Y,N): n(N)}=1 :- x(X) ,y(Y).

% defines 3x3 subgrids
subgrid(X,Y,A,B) :- x(X), x(A), y(Y), y(B),(X-1)/3 == (A-1)/3, (Y-1)/3 == (B-1)/3.

% every row can only contain each number from 1 to 9 once
:- sudoku(X,Y,N), sudoku(A,Y,N), X != A.
% every column can only contain each number from 1 to 9 once
:- sudoku(X,Y,N), sudoku(X,B,N), Y != B.
% every 3x3 subgrid can only contain each number from 1 to 9 once
:- sudoku(X,Y,V), sudoku(A,B,V), subgrid(X,Y,A,B), X != A, Y != B.
